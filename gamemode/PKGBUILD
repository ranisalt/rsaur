# Maintainer: Ysblokje <ysblokje at gmail dot com>
# Co-Maintainer: Mark Wagie <mark dot wagie at tutanota dot com>
# Contributor: Ranieri Althoff <ranisalt+aur at gmail dot com>

pkgbase=gamemode
pkgname=('gamemode' 'lib32-gamemode')
pkgver=1.6
pkgrel=3
url="https://github.com/FeralInteractive/gamemode"
license=('BSD 3-Clause License (Revised)')
arch=('x86_64')
depends=('polkit')
makedepends=('meson' 'lib32-glibc' 'lib32-dbus')
source=("$url/releases/download/$pkgver/$pkgbase-$pkgver.tar.xz"{,.asc})
validpgpkeys=('08F9C1C788CC7515')
sha256sums=('ede17eb042c1c87f7b35bfe96a00560afaea086f685d25bb3964d794b0af9c80'
            'SKIP')

build() {
  arch-meson "$pkgname-$pkgver" build64 \
    --libexecdir lib/gamemode \
    -Dwith-pam-group=gamemode \
    -Dwith-systemd-user-unit-dir=/usr/lib/systemd/user
  ninja -C build64

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  arch-meson "$pkgbase-$pkgver" build32 \
    -Dwith-sd-bus-provider=no-daemon \
    -Dwith-examples=false \
    -Dwith-util=false \
    --libdir lib32
  ninja -C build32
}

package_gamemode() {
  pkgdesc='A daemon/lib combo that allows games to request a set of optimisations be temporarily applied to the host OS'
  optdepends=('systemd: service unit')
  install='gamemode.install'

  DESTDIR="$pkgdir" ninja -C build64 install
  install -m644 -Dt "$pkgdir/usr/share/licenses/$pkgname" "$pkgbase-$pkgver/LICENSE.txt"
}

package_lib32-gamemode() {
  pkgdesc='A daemon/lib combo that allows games to request a set of optimisations be temporarily applied to the host OS (32-bit)'
  depends=('lib32-glibc' 'lib32-dbus')

  DESTDIR="$pkgdir" ninja -C build32 install
  rm -rfv "$pkgdir/usr/include"
  install -m644 -Dt "$pkgdir/usr/share/licenses/$pkgname" "$pkgbase-$pkgver/LICENSE.txt"
}
