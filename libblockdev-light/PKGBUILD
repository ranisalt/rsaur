# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libblockdev-light
pkgver=2.27
pkgrel=1
pkgdesc="A library for manipulating block devices"
arch=('x86_64')
url="https://github.com/rhinstaller/libblockdev"
license=('LGPL')
depends=('dosfstools' 'gptfdisk' 'libbytesize' 'ndctl'
         'parted' 'volume_key' 'xfsprogs' 'kmod' 'libyaml')
makedepends=('autoconf-archive' 'gobject-introspection' 'systemd' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rhinstaller/libblockdev/archive/$pkgver-1.tar.gz")
sha512sums=('d6c13d1e3d8aa2b1c721c13a387377c837dec0e7efc1b9a950204d8c879d420fbd57b2f8ab324a4685a8d0dae785a603ec3413d3e9e2987003c1258180bcf6e7')
_pkgname="${pkgname%-light}"
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")

build() {
  options=(
    --without-btrfs
    --without-dm
    --without-dmraid
    --without-mdraid
    --without-lvm
    --without-lvm_dbus
  )

  cd "$srcdir/$_pkgname-$pkgver-1"
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc ${options[@]}
  make
}

check() {
  cd "$srcdir/$_pkgname-$pkgver-1"
  make check
}

package() {
  cd "$srcdir/$_pkgname-$pkgver-1"
  make DESTDIR="$pkgdir" install
}
