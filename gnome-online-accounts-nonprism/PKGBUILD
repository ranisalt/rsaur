# Contributor (Arch): Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor (Arch): Ionut Biru <ibiru@archlinux.org>
# Contributor (Parabola): Andreas Grapentin <andreas@grapentin.org>
# Contributor (Parabola): MÃ¡rcio Silva <coadde@hyperbola.info>
# Contributor (Parabola): Isaac David <isacdaavid@at@isacdaavid@dot@info>

# parabola changes and rationale:
#  - removed support for possibly unsafe protocols

_pkgname=gnome-online-accounts
pkgname="$_pkgname-nonprism"
pkgver=3.44.0
pkgrel=1
pkgdesc="Single sign-on framework for GNOME"
url="https://wiki.gnome.org/Projects/GnomeOnlineAccounts"
arch=(x86_64)
license=(LGPL)
depends=(webkit2gtk json-glib libnotify rest libsecret krb5 gcr)
makedepends=(gobject-introspection gtk-doc vala git meson)
optdepends=('gvfs-goa: Virtual file systems, e.g. OwnCloud')
provides=("$_pkgname=$pkgver" libgoa-1.0.so libgoa-backend-1.0.so)
conflicts=("$_pkgname")
_commit=568139a2ce9d3791bd90b2b431898f712821c817  # tags/3.44.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-online-accounts.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $_pkgname build \
    -D exchange=false \
    -D facebook=false \
    -D flickr=false \
    -D foursquare=false \
    -D google=false \
    -D lastfm=false \
    -D windows_live=false \
    -D media_server=true \
    -D gtk_doc=true \
    -D man=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
