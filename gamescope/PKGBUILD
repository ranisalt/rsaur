_liftoff_commit=b08bbaa5e6331ed273c4bbd867143bf776c18207
_wlroots_commit=69c71dbc8afecc5da5c800cdc1475185064b4ac4

pkgname=gamescope
pkgver=3.8.4
pkgrel=1
pkgdesc='The micro-compositor formerly known as steamcompmgr'
arch=('x86_64')
url='https://github.com/Plagman/gamescope'
license=('BSD' 'custom:BSD 2-Clause "Simplified" License')
depends=('xorg-server-xwayland' 'libinput' 'libxkbcommon' 'libxcomposite'
         'libxtst' 'libxres' 'sdl2' 'xcb-util-renderutil')
makedepends=('git' 'meson' 'ninja' 'cmake' 'pixman' 'pkgconf' 'vulkan-headers'
             'wayland-protocols>=1.17')
source=("$pkgname-$pkgver.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "libliftoff-$_liftoff_commit.tar.gz::https://github.com/emersion/libliftoff/archive/$_liftoff_commit.tar.gz"
        "wlroots-$_wlroots_commit.tar.gz::https://github.com/swaywm/wlroots/archive/$_wlroots_commit.tar.gz")
sha256sums=('761d0baa20b683cb3f3bb0126229a05b2912e016970a24c3beba12468d7df6ae'
            '46415b8470913e3fe746867f396c45e59628cfd5918c0467aef27c86b9f5ecd6'
            'd2feb19cd2dcf1ea1ef3559a22ec49d916e6fc266bd80a5e32ab1564a5c93589')
provides=('steamcompmgr' 'libliftoff=0.0.0')
conflicts=('gamescope-git' 'libliftoff')
replaces=('steamcompmgr')

prepare() {
  cp -r "libliftoff-$_liftoff_commit/"* "$pkgname-$pkgver/subprojects/libliftoff/"
  sed -i -e "/subdir('test')/d" "$pkgname-$pkgver/subprojects/libliftoff/meson.build"

  cp -r "wlroots-$_wlroots_commit/"* "$pkgname-$pkgver/subprojects/wlroots/"
}

build() {
  CFLAGS="$CFLAGS -Wno-error"
  arch-meson --auto-features auto build "$pkgname-$pkgver"
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  rm -rfv "$pkgdir/usr/include"
  rm -rfv "$pkgdir/usr/lib/libwlroots"*
  rm -fv  "$pkgdir/usr/lib/pkgconfig/wlroots.pc"

  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/LICENSE"
}
