_liftoff_commit=24abeb923f52176808461e664776b56d52960d3e

pkgname=gamescope
pkgver=3.7.1
pkgrel=1
pkgdesc='The micro-compositor formerly known as steamcompmgr'
arch=('x86_64')
url='https://github.com/Plagman/gamescope'
license=('BSD' 'custom:BSD 2-Clause "Simplified" License')
depends=('wayland' 'opengl-driver' 'xorg-server-xwayland' 'libdrm' 'libinput'
         'libxkbcommon' 'libxcomposite' 'libcap' 'libxcb' 'libpng' 'glslang'
         'libxrender' 'libxtst' 'vulkan-icd-loader' 'sdl2' 'wlroots')
makedepends=('git' 'meson' 'ninja' 'cmake' 'pixman' 'pkgconf' 'vulkan-headers'
             'wayland-protocols>=1.17')
source=("${url}/archive/${pkgver}.tar.gz"
        "https://github.com/emersion/libliftoff/archive/$_liftoff_commit.tar.gz")
sha256sums=('044f7969b796e678415ca7762fb21c04ecb331660b442b4fdcc389410ef36c6f'
            'eccaafc0a007e61189e6cc5c0f2b6455aadc80a0dd81a79b73be4ba8f5149ab8')
provides=('steamcompmgr' 'libliftoff=0.0.0')
conflicts=('gamescope-git' 'libliftoff')
replaces=('steamcompmgr')

prepare() {
  cp -r "libliftoff-$_liftoff_commit/"* "$pkgname-$pkgver/subprojects/libliftoff"
  sed -i -e "/subdir('test')/d" "$pkgname-$pkgver/subprojects/libliftoff/meson.build"
}

build() {
  arch-meson "$pkgname-$pkgver" build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/LICENSE"
}
